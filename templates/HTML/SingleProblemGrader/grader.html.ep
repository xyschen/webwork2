% use WeBWorK::Utils 'wwRound';
%
<div class="problem-grader">
	<hr>
	<div class="problem-grader-table">
		% # Subscores for each answer in the problem.
		% if (@{ $grader->{pg}{flags}{ANSWER_ENTRY_ORDER} } > 1) {
			% # Determine the scores and weights for each part of the problem.
			% my $total = 0;
			% my (@scores, @weights);
			% for my $ans_id (@{ $grader->{pg}{flags}{ANSWER_ENTRY_ORDER} }) {
				% push(@scores,  wwRound(0, $grader->{pg}{answers}{$ans_id}{score} * 100));
				% push(@weights, $grader->{pg}{answers}{$ans_id}{weight} // 1);
				% $total += $weights[-1];
			% }
			%
			% # Normalize the weights
			% @weights = map { $_ / $total } @weights;
			%
			% for my $part (0 .. $#scores) {
				<div class="row align-items-center mb-2">
					<%= label_for "score_problem$grader->{problem_id}_$grader->{pg}{flags}{ANSWER_ENTRY_ORDER}[$part]",
						class => 'col-fixed col-form-label',
						begin =%>
						<%= maketext('Answer [_1] Score (%):', $part + 1) =%>
						<%= link_to '#',
							class => 'help-popup',
							data => {
								bs_content => maketext(
									'The initial value is the answer sub score for the answer '
										. 'that is currently shown.  If this is modified, it will be used to compute '
										. 'the total problem score below.  This score is not saved, and will reset to '
										. 'the score for the shown answer if the page is reloaded.'
								),
								bs_placement => 'top',
								bs_toggle    => 'popover'
							},
							begin =%>
							<i class="icon fas fa-question-circle" aria-hidden="true" data-alt="Help Icon"></i>
						<% end =%>
					<% end =%>
					<div class="col-sm">
						% param('answer-part-score', $scores[$part]);
						<%= number_field 'answer-part-score' => '',
							class => 'answer-part-score form-control form-control-sm d-inline',
							id    =>
								"score_problem$grader->{problem_id}_$grader->{pg}{flags}{ANSWER_ENTRY_ORDER}[$part]",
							data  => {
								problem_id    => $grader->{problem_id},
								answer_labels =>
									'["' . join('","', @{ $grader->{pg}{flags}{ANSWER_ENTRY_ORDER} }) . '"]',
								weight        => $weights[$part]
							},
							min          => 0,
							max          => 100,
							autocomplete => 'off' =%>
							&nbsp;
							<%== maketext('<b>Weight:</b> [_1]%', wwRound(2, $weights[$part] * 100)) =%>
					</div>
				</div>
			% }
		% }

		% # Total point value. Show only if configured to.
		% unless ($ce->{problemGraderScore} eq 'Percent') {
			% # Compute a reasonable step size based on point value.
			% # First use some preset nice values, then only use whole
			% # point values, such that the step size >= 1% of total.
			% my $stepSize;
			% if ($grader->{problem_value} == 1) {
				% $stepSize = 0.01;
			% } elsif ($grader->{problem_value} <= 5) {
				% $stepSize = 0.05;
			% } elsif ($grader->{problem_value} <= 10) {
				% $stepSize = 0.1;
			% } elsif ($grader->{problem_value} <= 25) {
				% $stepSize = 0.25;
			% } elsif ($grader->{problem_value} <= 50) {
				% $stepSize = 0.5;
			% } else {
				% $stepSize = int(($grader->{problem_value} - 1) / 100) + 1;
			% }
			% # Round point score to the nearest $stepSize.
			% param(
				% 'grader-problem-points',
				% wwRound(2, wwRound(0, $grader->{recorded_score} * $grader->{problem_value} / $stepSize) * $stepSize)
			% );
			<div class="row align-items-center mb-2">
				<%= label_for "score_problem$grader->{problem_id}_points",
					class => 'col-fixed col-form-label',
					begin =%>
					<%= maketext('Point Value (0 - [_1]):', $grader->{problem_value}) %>
					<%= link_to '#',
						class => 'help-popup',
						data => {
							bs_content => maketext(
								'The initial value is the currently saved score as a point value computed '
								. 'using the percent score.  This point value is used to compute the percent '
								. 'score (rounded to a whole percent) that can be saved.'
							)
							. (
								@{ $grader->{pg}{flags}{ANSWER_ENTRY_ORDER} } > 1
								? ' '
								. maketext(
									'This can computed from the answer sub scores above using the weights shown '
									. 'if they are modified.  Alternatively, enter the point score you want '
									. 'saved here (the above sub scores will be ignored).'
								)
								: ''
							),
							bs_placement => 'top',
							bs_toggle    => 'popover'
						},
						begin =%>
						<i class="icon fas fa-question-circle" aria-hidden="true" data-alt="Help Icon"></i>
						<span class="sr-only-glyphicon">Help Icon</span>
					<% end =%>
				<% end =%>
				<div class="col-sm">
					<%= number_field 'grader-problem-points' => '',
						min          => 0,
						max          => $grader->{problem_value},
						step         => $stepSize,
						autocomplete => 'off',
						id           => "score_problem$grader->{problem_id}_points",
						class        => "problem-points form-control form-control-sm d-inline",
						data         => { problem_id => $grader->{problem_id} } =%>
				</div>
			</div>
		% }

		% # Total problem score. (Hidden if configured to only show point values.)
		% if ($ce->{problemGraderScore} eq 'Point') {
			<%= hidden_field 'grader-problem-score' => wwRound(0, $grader->{recorded_score} * 100),
				id   => "score_problem$grader->{problem_id}",
				data => { problem_id => $grader->{problem_id} } =%>
		% } else {
			<div class="row align-items-center mb-2">
				<%= label_for "score_problem$grader->{problem_id}", class => 'col-fixed col-form-label', begin =%>
					<%= maketext('Problem Score (%):') %>
					<%= link_to '#',
						class => 'help-popup',
						data => {
							bs_content =>
							maketext('The initial value is the currently saved score for this student.')
							. (
								@{ $grader->{pg}{flags}{ANSWER_ENTRY_ORDER} } > 1
								? ' '
								. maketext(
									'This is the only part of the score that is actually saved. '
									. 'This is computed from the answer sub scores above using the weights shown if '
									. 'they are modified.  Alternatively, enter the score you want saved here '
									. '(the above sub scores will be ignored).'
								)
								: ''
							),
							bs_placement => 'top',
							bs_toggle    => 'popover'
						},
						begin =%>
						<i class="icon fas fa-question-circle" aria-hidden="true" data-alt="Help Icon"></i>
						<span class="sr-only-glyphicon">Help Icon</span>
					<% end =%>
				<% end =%>
				<div class="col-sm">
					% param('grader-problem-score', wwRound(0, $grader->{recorded_score} * 100));
					<%= number_field 'grader-problem-score' => '',
						min          => 0,
						max          => 100,
						autocomplete => "off",
						id           => "score_problem$grader->{problem_id}",
						class        => "problem-score form-control form-control-sm d-inline",
						data         => { problem_id => $grader->{problem_id} } =%>
				</div>
			</div>
		% }

		% # Instructor comment
		% if ($grader->{past_answer_id}) {
			<div class="row">
				<%= label_for "comment_problem$grader->{problem_id}" => maketext('Comment:'),
					class => 'col-fixed col-form-label' =%>
				<div class="col-sm">
					% param('grader-instructor-comment', $grader->{comment_string});
					<%= text_area 'grader-instructor-comment' => '',
						id    => "comment_problem$grader->{problem_id}",
						class => 'grader-problem-comment form-control d-inline latexentryfield',
						data  => { problem_id => $grader->{problem_id} },
						rows  => 3 =%>
					<%= hidden_field "MaThQuIlL_comment_problem$grader->{problem_id}" => '',
						id => "MaThQuIlL_comment_problem$grader->{problem_id}" =%>
				</div>
			</div>
		% }

		% # Save button
		<div class="row align-items-center">
			<div class="col-fixed mt-2">
				<button class="save-grade btn btn-sm btn-secondary" type="button"
					id="<%= "save_grade_problem$grader->{problem_id}" %>"
					data-course-id="<%= $grader->{course_id} %>"
					data-student-id="<%= $grader->{student_id} %>"
					data-set-id="<%= $grader->{set_id} %>"
					data-version-id="<%= $grader->{version_id} %>"
					data-problem-id="<%= $grader->{problem_id} %>"
					data-past-answer-id="<%= $grader->{past_answer_id} %>">
					<%= maketext('Save') =%>
				</button>
			</div>
			<div class="col-sm mt-2">
				<div id="<%= "grader_messages_problem$grader->{problem_id}" %>" class="problem-grader-message"></div>
			</div>
		</div>
	</div>
	<hr>
</div>
